apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 26

    // 使用 JDK 1.8
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 65
        versionName "6.5"
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:3.12.10'
    implementation 'androidx.lifecycle:lifecycle-common:2.1.0'
}

publish {
    userOrg = 'getactivity'
    groupId = 'com.hjq'
    artifactId = 'http'
    version = '6.5'
    description = 'Easy-to-use network request framework'
    website = "https://github.com/getActivity/EasyHttp"
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}